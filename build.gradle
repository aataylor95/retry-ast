buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
  }
}

apply plugin: 'com.jfrog.bintray'

apply plugin: 'groovy'

apply plugin: 'maven'
apply plugin: 'maven-publish'

apply plugin: 'application'
apply plugin: 'idea'

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencies {
  compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.7'

  testCompile ('org.spockframework:spock-core:1.1-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

group projectGroup
version projectVersion

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def pomConfig = {
  licenses {
    license {
      name "DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE Version 2"
      url "http://www.wtfpl.net/"
      distribution "repo"
    }
  }
  developers {
    developer {
      id properties.getProperty("bintrayUser")
      email properties.getProperty("email")
      name "Adam Taylor"
    }
  }

  scm {
    url projectRepo
  }
}

publishing {
  publications {
    mavenPublication(MavenPublication) {
      from components.java
      artifact sourcesJar {
        classifier "sources"
      }
      artifact javadocJar {
        classifier "javadoc"
      }
      groupId group
      artifactId projectName
      version projectVersion
      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Adds retry logic to methods using a Groovy AST Transformation')
        root.appendNode('name', 'Retry AST')
        root.appendNode('url', projectRepo)
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = properties.getProperty("bintrayUser")
  key = properties.getProperty("bintrayApikey")
  
  publications = ['mavenPublication']
  dryRun = false
  publish = true
  override = false
  
  pkg {
    repo = 'maven'
    name = 'retry-ast'
    licenses = ['WTFPL']
    vcsUrl = projectRepo
    githubRepo = 'aataylor95/retry-ast'
    githubReleaseNotesFile = 'README.md'
    
    version {
      name = projectVersion
      desc = 'Adds retry logic to methods using a Groovy AST Transformation'
      released = new Date()
      vcsTag = projectVersion
    }
  }
}